/* Forgotten Empires - Golden Pit (ver. October 13th) */

/* Note: random_map.def is automatically included in every rms script */

/* The Forgotten Fauna */

#const BRUCOUN 486
#const SUPAK 917
#const MLIKARNA 705
#const MAMALLAMA 305

/* The Forgotten Fauna End */

<PLAYER_SETUP>
random_placement
ai_info_map_type GOLD_RUSH 0 0 0

start_random
percent_chance 25 #define GOLDENPIT_JUNGLE /* vulture, llamas */
percent_chance 25 #define GOLDENPIT_WINTER /* cows, bears */
percent_chance 25 #define GOLDENPIT_NORTHERN 
percent_chance 25 #define GOLDENPIT_CLASSIC /* cows, bears */
end_random

<LAND_GENERATION>

if GOLDENPIT_JUNGLE
base_terrain LEAVES
elseif GOLDENPIT_WINTER
base_terrain SNOW
elseif GOLDENPIT_NORTHERN
base_terrain DIRT3
elseif GOLDENPIT_CLASSIC
base_terrain GRASS3
endif

create_player_lands
{
if GOLDENPIT_JUNGLE
terrain_type GRASS2
elseif GOLDENPIT_WINTER
terrain_type GRASS_SNOW
elseif GOLDENPIT_NORTHERN
terrain_type GRASS3
elseif GOLDENPIT_CLASSIC
terrain_type GRASS
endif
     land_percent 100
     base_size 10
     border_fuzziness 20
     clumping_factor 20
     base_elevation 7
}

create_land
{
if GOLDENPIT_WINTER
     terrain_type ICE
else
     terrain_type BEACH
endif
     land_percent 50
     border_fuzziness 10
     clumping_factor 15
     left_border 35
     right_border 35
     top_border 35
     bottom_border 35
}

<TERRAIN_GENERATION>

if GOLDENPIT_JUNGLE
create_terrain JUNGLE
elseif GOLDENPIT_WINTER
create_terrain SNOW_FOREST
elseif GOLDENPIT_NORTHERN
create_terrain PINE_FOREST
elseif GOLDENPIT_CLASSIC
create_terrain FOREST
endif
{
if GOLDENPIT_JUNGLE
base_terrain GRASS2
elseif GOLDENPIT_WINTER
base_terrain GRASS_SNOW
elseif GOLDENPIT_NORTHERN
base_terrain GRASS3
elseif GOLDENPIT_CLASSIC
base_terrain GRASS
endif
     spacing_to_other_terrain_types 5
     land_percent 15
     number_of_clumps 30
     set_avoid_player_start_areas
     set_scale_by_groups
}

if GOLDENPIT_JUNGLE
create_terrain DIRT3
elseif GOLDENPIT_WINTER
create_terrain DIRT_SNOW
elseif GOLDENPIT_NORTHERN
create_terrain DIRT
elseif GOLDENPIT_CLASSIC
create_terrain DIRT
endif
{
if GOLDENPIT_WINTER
     base_terrain ICE
else
     base_terrain BEACH
endif
     land_percent 100
     number_of_clumps 1
     spacing_to_other_terrain_types 7
     set_avoid_player_start_areas
     set_scale_by_groups
}

if GOLDENPIT_JUNGLE
create_terrain LEAVES
elseif GOLDENPIT_WINTER
create_terrain SNOW
elseif GOLDENPIT_NORTHERN
create_terrain DIRT3
elseif GOLDENPIT_CLASSIC
create_terrain GRASS3
endif
{
if GOLDENPIT_JUNGLE
base_terrain GRASS2
elseif GOLDENPIT_WINTER
base_terrain GRASS_SNOW
elseif GOLDENPIT_NORTHERN
base_terrain GRASS3
elseif GOLDENPIT_CLASSIC
base_terrain GRASS
endif
  number_of_clumps 80
  spacing_to_other_terrain_types 0
start_random
percent_chance 50
  land_percent 20
percent_chance 50
  land_percent 16
end_random
  set_scale_by_size
}

if GOLDENPIT_JUNGLE
create_terrain GRASS2
elseif GOLDENPIT_WINTER
create_terrain GRASS_SNOW
elseif GOLDENPIT_NORTHERN
create_terrain GRASS3
elseif GOLDENPIT_CLASSIC
create_terrain GRASS
endif
{
if GOLDENPIT_JUNGLE
base_terrain LEAVES
elseif GOLDENPIT_WINTER
base_terrain SNOW
elseif GOLDENPIT_NORTHERN
base_terrain DIRT3
elseif GOLDENPIT_CLASSIC
base_terrain GRASS3
endif
  number_of_clumps 50
  spacing_to_other_terrain_types 0
start_random
percent_chance 50
  land_percent 12
percent_chance 50
  land_percent 8
end_random
  set_scale_by_size
}

<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
     set_place_for_every_player
     group_placement_radius 18
     min_distance_to_players 0
     max_distance_to_players 0
}

create_object VILLAGER
{
     set_place_for_every_player
     min_distance_to_players 6
     max_distance_to_players 6
}

if REGICIDE

create_object VILLAGER
{
     number_of_objects 7
     set_place_for_every_player
     min_distance_to_players 6
     max_distance_to_players 6
}

endif

create_object SCOUT
{
     number_of_objects 1
     set_place_for_every_player
     min_distance_to_players 7
     max_distance_to_players 9
}

if REGICIDE

create_object KING
{
     set_place_for_every_player
     min_distance_to_players 6
     max_distance_to_players 6
}

create_object CASTLE
{
     set_place_for_every_player
     min_distance_to_players 9
     max_distance_to_players 12
}

endif

if GOLDENPIT_JUNGLE
start_random
percent_chance 50
create_object TURKEY
percent_chance 50
create_object MAMALLAMA
end_random
elseif GOLDENPIT_NORTHERN
create_object SHEEP
else
start_random
percent_chance 50
create_object MLIKARNA
percent_chance 50
create_object SHEEP
end_random
endif
{
   number_of_objects 4
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 9
   max_distance_to_players 11
}

if GOLDENPIT_JUNGLE
start_random
percent_chance 50
create_object TURKEY
percent_chance 50
create_object MAMALLAMA
end_random
elseif GOLDENPIT_NORTHERN
create_object SHEEP
else
start_random
percent_chance 50
create_object MLIKARNA
percent_chance 50
create_object SHEEP
end_random
endif
{
   number_of_objects 2
number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 14
   max_distance_to_players 24
}

create_object DEER
{
start_random
percent_chance 50
  number_of_objects 4
percent_chance 50
  number_of_objects 3
end_random
  group_placement_radius 3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
start_random
percent_chance 50
  min_distance_to_players 14
  max_distance_to_players 18
percent_chance 50
  min_distance_to_players 25
  max_distance_to_players 28
end_random
}

if GOLDENPIT_JUNGLE
create_object JAVELINA
else
create_object BOAR
endif
{
     number_of_objects          1
     number_of_groups 		1
     set_loose_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players    16
     max_distance_to_players    22
}

if GOLDENPIT_JUNGLE
create_object JAVELINA
else
create_object BOAR
endif
{
     number_of_objects          1
     number_of_groups 		1
     set_loose_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players    16
     max_distance_to_players    22
}

create_object GOLD
{
     number_of_objects 3
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 12
     max_distance_to_players 15
     min_distance_group_placement 6
     max_distance_to_other_zones 7
}

create_object GOLD
{
     number_of_objects 2
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 19
     max_distance_to_players 25
     min_distance_group_placement 6
     max_distance_to_other_zones 7
}

create_object STONE
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 12
     max_distance_to_players 15
     min_distance_group_placement 6
     max_distance_to_other_zones 7
}

create_object STONE
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 20
     max_distance_to_players 24
     min_distance_group_placement 6
     max_distance_to_other_zones 7
}

create_object FORAGE
{
  number_of_objects 6
  number_of_groups 1
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 12
  min_distance_group_placement 6
}

if GOLDENPIT_JUNGLE
create_object JUNGLETREE
elseif GOLDENPIT_WINTER
create_object SNOWPINETREE
elseif GOLDENPIT_NORTHERN
create_object PINETREE
elseif GOLDENPIT_CLASSIC
create_object OAKTREE
endif
{
     number_of_objects             2
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       4
     max_distance_to_players       5
     min_distance_group_placement  2
}

if GOLDENPIT_JUNGLE
create_object JUNGLETREE
elseif GOLDENPIT_WINTER
create_object SNOWPINETREE
elseif GOLDENPIT_NORTHERN
create_object PINETREE
elseif GOLDENPIT_CLASSIC
create_object OAKTREE
endif
{
     number_of_objects             3
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       6
     max_distance_to_players       8
     min_distance_group_placement  2
}

if LUDIKRIS_MAP
create_object RELIC
{
   number_of_objects            20
   min_distance_to_players     25
   temp_min_distance_group_placement 20
}
else
create_object RELIC
{
   number_of_objects            5
   min_distance_to_players     25
   temp_min_distance_group_placement 20
}
endif

if GOLDENPIT_JUNGLE
create_object JAGUAR
elseif GOLDENPIT_NORTHERN
create_object WOLF
else
start_random
percent_chance 50
create_object WOLF
percent_chance 50
create_object BRUCOUN
end_random
endif
{
     number_of_groups 3
     set_loose_grouping
        number_of_objects 1
  set_scaling_to_map_size
     set_gaia_object_only
     min_distance_group_placement 12
     min_distance_to_players 42
}

if GOLDENPIT_JUNGLE
start_random
percent_chance 50
create_object MACAW
percent_chance 50
create_object SUPAK
end_random
else
create_object HAWK
endif
{
     number_of_objects 4
     set_scaling_to_map_size
}

if GOLDENPIT_JUNGLE
create_object JUNGLETREE
elseif GOLDENPIT_WINTER
create_object SNOWPINETREE
elseif GOLDENPIT_NORTHERN
create_object PINETREE
elseif GOLDENPIT_CLASSIC
create_object OAKTREE
endif
{
  number_of_objects          20
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
if GOLDENPIT_JUNGLE
terrain_to_place_on GRASS2
elseif GOLDENPIT_WINTER
terrain_to_place_on GRASS_SNOW
elseif GOLDENPIT_NORTHERN
terrain_to_place_on GRASS3
elseif GOLDENPIT_CLASSIC
terrain_to_place_on GRASS
endif
}

create_object GOLD
{
if LUDIKRIS_MAP
	number_of_groups 36
	number_of_objects 10
else
	number_of_groups 12
	number_of_objects 10
endif
	group_placement_radius			2
	set_tight_grouping
	set_gaia_object_only
	min_distance_group_placement 		7
	min_distance_to_players 		10
if GOLDENPIT_JUNGLE
terrain_to_place_on DIRT3
elseif GOLDENPIT_WINTER
terrain_to_place_on DIRT_SNOW
elseif GOLDENPIT_NORTHERN
terrain_to_place_on DIRT
elseif GOLDENPIT_CLASSIC
terrain_to_place_on DIRT
endif
}

create_object STONE
{
if LUDIKRIS_MAP
	number_of_groups 18
	number_of_objects 8
else
	number_of_groups 5
	number_of_objects 6
endif
	group_placement_radius			2
	set_tight_grouping
	set_gaia_object_only
	min_distance_group_placement 		3
	min_distance_to_players 		10
if GOLDENPIT_JUNGLE
terrain_to_place_on DIRT3
elseif GOLDENPIT_WINTER
terrain_to_place_on DIRT_SNOW
elseif GOLDENPIT_NORTHERN
terrain_to_place_on DIRT
elseif GOLDENPIT_CLASSIC
terrain_to_place_on DIRT
endif
}

if TINY_MAP
elseif SMALL_MAP
elseif MEDIUM_MAP
else
create_object FORAGE
{
	number_of_groups			2
	number_of_objects			5
	group_placement_radius			3
	set_tight_grouping
	set_gaia_object_only
	min_distance_to_players			45
	temp_min_distance_group_placement	20
}
endif

create_object DEER
{
	number_of_objects			4
	number_of_groups			3
	set_loose_grouping
	set_gaia_object_only
	min_distance_to_players			45
	min_distance_group_placement		20
}

<ELEVATION_GENERATION>

