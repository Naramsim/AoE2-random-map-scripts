/* Forgotten Empires - Bonus Map: Hamburger (October 13th) */

/* Note: random_map.def is automatically included in every rms script */

/* The Forgotten Fauna */

#const BRUCOUN 486
#const SUPAK 917
#const MLIKARNA 705
#const MAMALLAMA 305

/* The Forgotten Fauna End */

start_random
percent_chance 20 #define SMRK
percent_chance 20 #define PRALES
percent_chance 20 #define KAKTUS
percent_chance 20 #define KOPRIVA
percent_chance 20 #define RAMPOUCH
end_random

if SMRK
#const FOREST_A 21	
#const FOREST_B 19	
#const FOREST_C 10
#const LAND_A 0		
#const LAND_B 12	
elseif PRALES
#const FOREST_A 13	
#const FOREST_B 17	
#const FOREST_C 17
#const LAND_A 12	
#const LAND_B 0		
elseif KAKTUS
#const FOREST_A 10	
#const FOREST_B 13	
#const FOREST_C 13	
#const LAND_A 14	
#const LAND_B 6		
elseif KOPRIVA
#const FOREST_A 19	
#const FOREST_B 10	
#const FOREST_C 10	
#const LAND_A 0	
#const LAND_B 9		
elseif RAMPOUCH
#const FOREST_A 10	
#const FOREST_B 21	
#const FOREST_C 19	
#const LAND_A 32	
#const LAND_B 34	
endif


<PLAYER_SETUP>
  random_placement

<LAND_GENERATION>

base_terrain FOREST_A

create_player_lands
{
     terrain_type LAND_A
     land_percent 100
     base_size 9
     border_fuzziness 100
     clumping_factor 20
     set_zone_by_team
right_border 20
bottom_border 20
left_border 20
top_border 20
     other_zone_avoidance_distance 4
}

<TERRAIN_GENERATION>

create_terrain WATER
{
base_terrain FOREST_A
land_percent 100
}

create_terrain LAND_B
{
base_terrain WATER
number_of_clumps 10
spacing_to_other_terrain_types 9
land_percent 60
}

create_terrain FOREST_C
{
base_terrain LAND_B
number_of_clumps 100
spacing_to_other_terrain_types 6
land_percent 8
}

create_terrain MED_WATER
{
base_terrain WATER
number_of_clumps 10
spacing_to_other_terrain_types 2
land_percent 40
}

create_terrain DEEP_WATER
{
base_terrain MED_WATER
number_of_clumps 6
spacing_to_other_terrain_types 3
land_percent 20
}

<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius     18
  min_distance_to_players    0
  max_distance_to_players    0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
 }

create_object SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
 }

if REGICIDE
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}

endif

if PRALES
create_object MAMALLAMA
elseif RAMPOUCH
create_object MLIKARNA
elseif SMRK
create_object MLIKARNA
else
create_object SHEEP
endif
{
   number_of_objects 4
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 2
   max_distance_to_players 12
}

if PRALES
create_object MAMALLAMA
elseif RAMPOUCH
create_object MLIKARNA
elseif SMRK
create_object MLIKARNA
else
create_object SHEEP
endif
{
   number_of_objects 2
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 4
   max_distance_to_players 8
}

if PRALES
create_object MAMALLAMA
elseif RAMPOUCH
create_object MLIKARNA
elseif SMRK
create_object MLIKARNA
else
create_object SHEEP
endif
{
   number_of_objects 2
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 4
   max_distance_to_players 8
}

create_object DEER
{
  number_of_objects 5
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 3
  max_distance_to_players 10
}

if PRALES
create_object JAVELINA
else
create_object BOAR
endif
{
   number_of_objects 1
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 3
  max_distance_to_players 12
}

if PRALES
create_object JAVELINA
else
create_object BOAR
endif
{
  number_of_objects 1
  number_of_groups 1
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 3
  max_distance_to_players 12
}

/* HOME GOLD */

create_object GOLD
{
  number_of_groups 2
  number_of_objects 6
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 8
  max_distance_to_players 12
  min_distance_group_placement 1
  max_distance_to_other_zones 7
}

/* DISTANT GOLD */

create_object GOLD
{
  number_of_groups 180
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 14
  temp_min_distance_group_placement 45
  max_distance_to_other_zones 2
  terrain_to_place_on LAND_B
}


/* HOME STONE */

create_object STONE
{
  number_of_groups 2
  number_of_objects 4
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 8
  max_distance_to_players 12
  min_distance_group_placement 1
  max_distance_to_other_zones 7
}

/* DISTANT STONE */

create_object STONE
{
  number_of_groups 150
  number_of_objects 4
  group_placement_radius 2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 14
  temp_min_distance_group_placement 55
  max_distance_to_other_zones 2
  terrain_to_place_on LAND_B
}

create_object RELIC
{
  number_of_objects 1
  number_of_groups 150
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players 25
  temp_min_distance_group_placement 79
}

/* NEAR FORAGE */

create_object FORAGE
{
  number_of_objects 2
  number_of_groups 1
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 16
  min_distance_group_placement 6
}

if SMRK
create_object PINETREE
elseif PRALES
create_object JUNGLETREE
elseif KAKTUS
create_object PALMTREE
elseif KOPRIVA
create_object OAKTREE
elseif RAMPOUCH
create_object SNOWPINETREE
endif
{
  number_of_objects 2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 4
  max_distance_to_players 5
  min_distance_group_placement 2
}

if SMRK
create_object PINETREE
elseif PRALES
create_object JUNGLETREE
elseif KAKTUS
create_object PALMTREE
elseif KOPRIVA
create_object OAKTREE
elseif RAMPOUCH
create_object SNOWPINETREE
endif
{
  number_of_objects 3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 6
  max_distance_to_players 8
  min_distance_group_placement 2
}

/* Captain Fin */

create_object DORADO
{
  number_of_objects 600
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones 5
     min_distance_group_placement 5
}

create_object SHORE_FISH
{
  number_of_objects 25
     set_scaling_to_map_size
     min_distance_group_placement 5
     set_gaia_object_only
}

/* Captain Fin end */

if PRALES
create_object MACAW
elseif KAKTUS
create_object SUPAK
else
create_object HAWK
endif
{
     number_of_objects 4
     set_scaling_to_map_size
}

#const ICY_SHORE 37

if TINY_MAP

<CONNECTION_GENERATION>

create_connect_all_players_land
{
  replace_terrain WATER FOREST_B
  replace_terrain MED_WATER FOREST_B
  replace_terrain DEEP_WATER FOREST_B
  replace_terrain BEACH FOREST_B
  replace_terrain ICY_SHORE FOREST_B
  terrain_cost WATER 7
  terrain_cost MED_WATER 9
  terrain_cost DEEP_WATER 15
  terrain_cost LAND_A 3
  terrain_cost FOREST 7
  terrain_cost FOREST_B 7
  terrain_cost BEACH 7
  terrain_cost ICY_SHORE 7
  terrain_cost DESERT 1
  terrain_size WATER 15 1
  terrain_size MED_WATER 15 1
  terrain_size DEEP_WATER 15 1
  terrain_size BEACH 15 1
  terrain_size ICY_SHORE 15 1
  terrain_size LAND_A 0 0
  terrain_size FOREST 0 0
  terrain_size FOREST_B 0 0
}
else

<CONNECTION_GENERATION>

create_connect_all_players_land /* or create_connect_teams_lands or create_connect_all_lands */
{
     replace_terrain BEACH FOREST_B
     replace_terrain FOREST_B FOREST_B
     replace_terrain WATER FOREST_B
     replace_terrain MED_WATER FOREST_B
     replace_terrain ICY_SHORE FOREST_B
     terrain_cost FOREST_B 2
     terrain_cost WATER 7
     terrain_cost MED_WATER 9
     terrain_size FOREST_B 21 0
}
endif