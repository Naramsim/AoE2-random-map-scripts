
/* Oasis for Forgotten Empires */

/* Note: random_map.def is automatically included in every rms script */
<PLAYER_SETUP>
  random_placement 
	ai_info_map_type OASIS 0 0 0

<LAND_GENERATION>

start_random
  percent_chance 40
  #define DESERT_MAP
  percent_chance 30
  #define FROZEN_MAP
end_random

if DESERT_MAP
  base_terrain DIRT
elseif FROZEN_MAP
  base_terrain SNOW
else
  base_terrain GRASS
endif

/* CREATE THE FOREST */

create_land 
{
if FROZEN_MAP
  terrain_type                     SNOW_FOREST
else
  terrain_type                     PALM_DESERT
endif
  land_percent                     60
  left_border                      20
  right_border                     20
  top_border                       20
  bottom_border                    20 
}

/* CREATE THE POOL */

create_land 
{
if FROZEN_MAP
  terrain_type                     ICE
else
  terrain_type                     WATER
endif
  land_percent                     5
  left_border                      40
  right_border                     40
  top_border                       40
  bottom_border                    40 
 }

create_player_lands 
{            
if DESERT_MAP
  terrain_type                     DIRT
elseif FROZEN_MAP
  terrain_type                     SNOW
else           
  terrain_type                     GRASS
endif
  land_percent                     30
  base_size                         7
  zone 1
  other_zone_avoidance_distance    5 
  border_fuzziness                 10
}

/* ****************************************************** */

<TERRAIN_GENERATION>

create_terrain MED_WATER
{
base_terrain WATER
number_of_clumps               3
spacing_to_other_terrain_types 1
land_percent 2
}

create_terrain DEEP_WATER
{
base_terrain MED_WATER
number_of_clumps               6
spacing_to_other_terrain_types 5
land_percent 1
}


/* SECONDARY FOREST */

if DESERT_MAP
  create_terrain PALM_DESERT
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 5
  land_percent                   3
  number_of_clumps               12
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif FROZEN_MAP
  create_terrain SNOW_FOREST
{
  base_terrain                   SNOW
  spacing_to_other_terrain_types 5
  land_percent                   3
  number_of_clumps               12
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain FOREST
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 5
  land_percent                   3
  number_of_clumps               12
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* PRIMARY PATCH */

if DESERT_MAP
   create_terrain GRASS
{
  base_terrain                   DIRT
  number_of_clumps               8
  spacing_to_other_terrain_types 1
  land_percent                   4
  set_scale_by_size
}
elseif FROZEN_MAP
   create_terrain DIRT_SNOW
{
  base_terrain                   SNOW
  number_of_clumps               8
  spacing_to_other_terrain_types 1
  land_percent                   4
  set_scale_by_size
}
else
create_terrain DIRT
{
  base_terrain                   GRASS
  number_of_clumps               8
  spacing_to_other_terrain_types 1
  land_percent                   9
  set_scale_by_size
}
endif

/* SECONDARY PATCH */

if DESERT_MAP
create_terrain DESERT
{
  base_terrain                   DIRT
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   5
  set_scale_by_size
}
elseif FROZEN_MAP
create_terrain GRASS_SNOW
{
  base_terrain                   SNOW
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   5
  set_scale_by_size
}
else
create_terrain GRASS3
{
  base_terrain                   GRASS
  number_of_clumps               24
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
endif

/* TERTIARY PATCH */

if DESERT_MAP
   create_terrain GRASS3
{
  base_terrain                   DIRT
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
elseif FROZEN_MAP
   create_terrain DIRT
{
  base_terrain                   DIRT_SNOW
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
else
create_terrain DIRT3
{
  base_terrain                   GRASS
  number_of_clumps               30
  spacing_to_other_terrain_types 1
  land_percent                   2
  set_scale_by_size
}
endif


<OBJECTS_GENERATION>


create_object TOWN_CENTER
{
  set_place_for_every_player
  group_placement_radius     18
  min_distance_to_players    0
  max_distance_to_players    0
}

create_object VILLAGER
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
 }

create_object SCOUT
{
  number_of_objects             1
  set_place_for_every_player
  min_distance_to_players       7
  max_distance_to_players       9
 }

if LUDIKRIS_MAP
create_object RELIC
{
   number_of_objects            250
   min_distance_to_players     25
   temp_min_distance_group_placement 110
}
else
create_object RELIC
{
   number_of_objects            5
   min_distance_to_players     25
   temp_min_distance_group_placement 20
}
endif



if REGICIDE
create_object VILLAGER
{
  number_of_objects             7
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object KING
{
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       6
}

create_object CASTLE
{
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    10
}

endif

/* NEAR FORAGE */

create_object FORAGE
{
  number_of_objects          6
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    10
  max_distance_to_players    12
  min_distance_group_placement  6
}

/* NEAR GOLD */

create_object GOLD
{
  number_of_objects          7
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    12
  max_distance_to_players    16
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* MEDIUM GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    18
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR GOLD */

create_object GOLD
{
  number_of_objects          4
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    25
  max_distance_to_players    35
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* NEAR STONE */

create_object STONE
{
  number_of_objects          5
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    14
  max_distance_to_players    18
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* FAR STONE */

create_object STONE
{
  number_of_objects          4
  group_placement_radius     2  
  set_tight_grouping 
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    20
  max_distance_to_players    26
  min_distance_group_placement  7
  max_distance_to_other_zones  7
}

/* BAA BAA */

create_object SHEEP
{
   number_of_objects 4
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    9
   max_distance_to_players    11
} 
create_object SHEEP
{
   number_of_objects 2
   number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    14
   max_distance_to_players    30
}

create_object DEER
{
start_random
percent_chance 50
  number_of_objects          4
percent_chance 50
  number_of_objects          3
end_random
  group_placement_radius     3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
start_random
percent_chance 50
  min_distance_to_players    14
  max_distance_to_players    22
percent_chance 50
  min_distance_to_players    23
  max_distance_to_players    30
end_random
}

if TROPICAL_MAP
create_object JAVELINA
else
create_object BOAR
endif
{
  number_of_objects          1
  set_loose_grouping  
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

if TROPICAL_MAP
create_object JAVELINA
else
create_object BOAR
endif
{
  number_of_objects          1
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players    16
  max_distance_to_players    22
}

create_object WOLF
{
  number_of_objects          2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_group_placement 12
  min_distance_to_players    30
  max_distance_to_players    40
}

/* RESOURCES BY MAP */

if LARGE_MAP
create_object FORAGE
{
  number_of_groups           1    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  7
}
endif

if HUGE_MAP
create_object FORAGE
{
  number_of_groups           2    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  7
}
endif

if GIGANTIC_MAP
create_object FORAGE
{
  number_of_groups           3    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  max_distance_to_players       120
  min_distance_group_placement  7
}
endif

if LUDIKRIS_MAP
create_object FORAGE
{
  number_of_groups           12    
  number_of_objects          5
  group_placement_radius     3
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  temp_min_distance_group_placement  15
}
endif

/* EXTRA GOLD */

if TINY_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object GOLD
{
  number_of_groups           2    
  number_of_objects          3
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object GOLD
{
  number_of_groups           3    
  number_of_objects          3
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object GOLD
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object GOLD
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LUDIKRIS_MAP
create_object GOLD
{
  number_of_groups           250
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  temp_min_distance_group_placement  44
}
endif


/* EXTRA STONE */

if TINY_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if SMALL_MAP
create_object STONE
{
  number_of_groups           1    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if MEDIUM_MAP
create_object STONE
{
  number_of_groups           2    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LARGE_MAP
create_object STONE
{
  number_of_groups           3    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if HUGE_MAP
create_object STONE
{
  number_of_groups           4    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if GIGANTIC_MAP
create_object STONE
{
  number_of_groups           5    
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  min_distance_group_placement  9
}
endif

if LUDIKRIS_MAP
create_object STONE
{
  number_of_groups           250
  number_of_objects          4
  group_variance             1
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  min_distance_to_players       40
  temp_min_distance_group_placement  44
}
endif

if DESERT_MAP
create_object PALMTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object PALMTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
elseif ALPINE_MAP
create_object PINETREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object PINETREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
elseif ASIAN_MAP
create_object BAMBOO_TREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object PINETREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
elseif TROPICAL_MAP
create_object PALMTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object OAKTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
elseif FROZEN_MAP
create_object SNOWPINETREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object SNOWPINETREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
else
create_object OAKTREE
{
  number_of_objects             2
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       4
  max_distance_to_players       5
  min_distance_group_placement  2
}
create_object OAKTREE
{
  number_of_objects             3
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players       6
  max_distance_to_players       8
  min_distance_group_placement  2
}
endif

create_object HAWK
{
   number_of_objects         4
   set_scaling_to_map_size
}

create_object WOLF
{
  number_of_groups          3
  set_loose_grouping
  start_random
  percent_chance 75
	number_of_objects 1
  percent_chance 25
	number_of_objects 2
  end_random
  set_scaling_to_map_size
  set_gaia_object_only
  min_distance_group_placement 12
  min_distance_to_players    40
  max_distance_to_players    120
} 



create_object SHEEP
{
   number_of_objects 3
   number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    35
   min_distance_group_placement  5
}

if DESERT_MAP
create_object PALMTREE
{
  number_of_objects          40
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
elseif FROZEN_MAP
create_object SNOWPINETREE
{
  number_of_objects          40
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
else
create_object OAKTREE
{
  number_of_objects          40
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
endif


/* ****************************************************** */

<ELEVATION_GENERATION>

create_elevation        7
{
if DESERT_MAP
  base_terrain                DIRT
elseif FROZEN_MAP
  base_terrain                SNOW
else           
  base_terrain                GRASS
endif
  number_of_clumps 		14
  number_of_tiles 		2000
  set_scale_by_groups
  set_scale_by_size           
}



