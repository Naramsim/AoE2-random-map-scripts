/* Forgotten Empires - Hideout (ver. October 13th) */

/* Note: random_map.def is automatically included in every rms script */

/* The Forgotten Fauna */

#const BRUCOUN 486
#const SUPAK 917
#const MLIKARNA 705
#const MAMALLAMA 305

/* The Forgotten Fauna End */

<PLAYER_SETUP>
  random_placement 
  ai_info_map_type ARABIA 0 0 0

<LAND_GENERATION>

base_terrain GRASS

create_player_lands
{
  terrain_type LEAVES
  land_percent 16
  base_size 12
  top_border 18
  bottom_border 18
  right_border 18
  left_border 18
  clumping_factor 15
  other_zone_avoidance_distance 16
}

create_land
{
  terrain_type FOREST
  land_percent 90
  top_border 20
  bottom_border 20
  right_border 20
  left_border 20
  clumping_factor 15
  border_fuzziness 45
  zone 52
  other_zone_avoidance_distance 0
}

<TERRAIN_GENERATION>

create_terrain PINE_FOREST
{
  base_terrain GRASS
  spacing_to_other_terrain_types 0
  land_percent 4
  number_of_clumps 6
  set_avoid_player_start_areas
  set_scale_by_groups
}

create_terrain GRASS3
{
  base_terrain GRASS
  number_of_clumps 8
  spacing_to_other_terrain_types 0
  land_percent 6
  set_scale_by_size
}

create_terrain DIRT3
{
  base_terrain LEAVES
  number_of_clumps 24
  spacing_to_other_terrain_types 0
  land_percent 5
  set_scale_by_size
}

create_terrain GRASS
{
  base_terrain LEAVES
  number_of_clumps 30
  spacing_to_other_terrain_types 0
  land_percent 2
  set_scale_by_size
}

create_terrain FOREST
{
  base_terrain LEAVES
  number_of_clumps 10
  spacing_to_other_terrain_types 2
number_of_tiles 20
  set_avoid_player_start_areas
  set_scale_by_size
}

create_terrain PINE_FOREST
{
  base_terrain FOREST
land_percent 8
  number_of_clumps 100
}

<OBJECTS_GENERATION>

create_object TOWN_CENTER
{
     set_place_for_every_player
     group_placement_radius 18
     min_distance_to_players 0
     max_distance_to_players 0
}

create_object PALISADE_WALL
{
  set_place_for_every_player
  min_distance_to_players 17
  max_distance_to_players 17
 }

create_object VILLAGER
{
     set_place_for_every_player
     min_distance_to_players 6
     max_distance_to_players 6
}

if REGICIDE

create_object VILLAGER
{
     number_of_objects 7
     set_place_for_every_player
     min_distance_to_players 6
     max_distance_to_players 6
}

endif

create_object SCOUT
{
     number_of_objects 1
     set_place_for_every_player
     min_distance_to_players 7
     max_distance_to_players 9
}

if REGICIDE

create_object KING
{
     set_place_for_every_player
     min_distance_to_players 6
     max_distance_to_players 6
}

create_object CASTLE
{
     set_place_for_every_player
     min_distance_to_players 9
     max_distance_to_players 12
}

endif

start_random
percent_chance 50
#define COWSCOWS
end_random

if COWSCOWS
create_object MLIKARNA
else
create_object SHEEP
endif
{
   number_of_objects 4
   number_of_groups 1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 9
   max_distance_to_players 11
}

if COWSCOWS
create_object MLIKARNA
else
create_object SHEEP
endif
{
   number_of_objects 2
number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players 14
   max_distance_to_players 24
}

create_object DEER
{
start_random
percent_chance 50
  number_of_objects 4
percent_chance 50
  number_of_objects 3
end_random
  group_placement_radius 3
  set_loose_grouping
  set_gaia_object_only
  set_place_for_every_player
start_random
percent_chance 50
  min_distance_to_players 14
  max_distance_to_players 16
percent_chance 50
  min_distance_to_players 23
  max_distance_to_players 26
end_random
}

create_object BOAR
{
     number_of_objects          1
     number_of_groups 		1
     set_loose_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players    16
     max_distance_to_players    22
}


create_object BOAR
{
     number_of_objects          1
     number_of_groups 		1
     set_loose_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players    16
     max_distance_to_players    22
}


create_object GOLD
{
     number_of_objects 5
     number_of_groups 2
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 12
     max_distance_to_players 14
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object GOLD
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 19
     max_distance_to_players 25
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object STONE
{
     number_of_objects 4
     number_of_groups 2
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 12
     max_distance_to_players 14
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object STONE
{
     number_of_objects 4
     number_of_groups 1
     group_placement_radius 3
     set_tight_grouping
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players 20
     max_distance_to_players 24
     min_distance_group_placement 4
     max_distance_to_other_zones 7
}

create_object FORAGE
{
  number_of_objects 6
  number_of_groups 1
  group_placement_radius 3
  set_tight_grouping
  set_gaia_object_only
  set_place_for_every_player
  min_distance_to_players 10
  max_distance_to_players 13
  min_distance_group_placement 6
}

create_object OAKTREE
{
     number_of_objects             2
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       4
     max_distance_to_players       5
     min_distance_group_placement  2
}

create_object PINETREE
{
     number_of_objects             3
     set_gaia_object_only
     set_place_for_every_player
     min_distance_to_players       6
     max_distance_to_players       8
     min_distance_group_placement  2
}

/* Far gold & stone */


create_object GOLD
{
if TINY_MAP
     number_of_groups           2
     number_of_objects          4
elseif SMALL_MAP
     number_of_groups           2
     number_of_objects          4
elseif MEDIUM_MAP
     number_of_groups           3
     number_of_objects          4
elseif LARGE_MAP
     number_of_groups           4
     number_of_objects          5
elseif HUGE_MAP
     number_of_groups           5
     number_of_objects          5
elseif GIGANTIC_MAP
     number_of_groups           6
     number_of_objects          6
else
     number_of_groups           24
     number_of_objects          6
endif
     group_placement_radius     2
     set_tight_grouping
     set_gaia_object_only
     min_distance_to_players       30
     min_distance_group_placement  10
}

create_object STONE
{
if TINY_MAP
     number_of_groups           2
     number_of_objects          3
elseif SMALL_MAP
     number_of_groups           2
     number_of_objects          3
elseif MEDIUM_MAP
     number_of_groups           3
     number_of_objects          4
elseif LARGE_MAP
     number_of_groups           4
     number_of_objects          4
elseif HUGE_MAP
     number_of_groups           5
     number_of_objects          4
elseif GIGANTIC_MAP
     number_of_groups           6
     number_of_objects          4
else
     number_of_groups           24
     number_of_objects          4
endif
     group_placement_radius     2
     set_tight_grouping
     set_gaia_object_only
     min_distance_to_players       30
     min_distance_group_placement  10
}

if LUDIKRIS_MAP
create_object RELIC
{
     number_of_objects 20
      min_distance_to_players 25
     temp_min_distance_group_placement 20
}
else
create_object RELIC
{
     number_of_objects 5
      min_distance_to_players 25
     temp_min_distance_group_placement 20
}
endif

start_random
percent_chance 50
#define BEARBEAR
end_random

if BEARBEAR
create_object BRUCOUN
else
create_object WOLF
endif
{
     number_of_groups 3
     set_loose_grouping
        number_of_objects 1
  set_scaling_to_map_size
     set_gaia_object_only
     min_distance_group_placement 12
     min_distance_to_players 40
}

create_object HAWK
{
     number_of_objects 4
     set_scaling_to_map_size
}

create_object OAKTREE
{
  number_of_objects          15
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}

create_object PINETREE
{
  number_of_objects          15
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}


<ELEVATION_GENERATION>

create_elevation        4
{  
  base_terrain                     GRASS
  number_of_clumps 		15
  number_of_tiles 		800
  set_scale_by_groups
  set_scale_by_size           
}

create_elevation        4
{  
  base_terrain                     DIRT
  number_of_clumps 		15
  number_of_tiles 		800
  set_scale_by_groups
  set_scale_by_size           
}
