/* ********* RIVERS ********** */
/* 21 DEC 99 */

/* Note: random_map.def is automatically included in every rms script */

/* ****************************************************** */
<PLAYER_SETUP>
  random_placement  /* note this is only currently valid entry */

/* ****************************************************** */
<LAND_GENERATION>
base_terrain WATER

start_random
  percent_chance 20
  #define DESERT_MAP
  percent_chance 20
  #define ASIAN_MAP
end_random

create_player_lands 
{            
if DESERT_MAP
  terrain_type                     DIRT
elseif ASIAN_MAP
  terrain_type                     GRASS2
else           
  terrain_type                     GRASS
endif
  land_percent                     65
  base_size                        11
start_random
  percent_chance 50
    left_border                    8
  percent_chance 50
    right_border                   8
end_random
start_random
  percent_chance 50
    top_border                     8
  percent_chance 50
    bottom_border                  8
end_random
border_fuzziness                   7
other_zone_avoidance_distance      5
clumping_factor                    15
}

/* BONUS ISLAND */

create_land 
{
  if DESERT_MAP
    terrain_type                     DIRT
  elseif ASIAN_MAP
    terrain_type                     GRASS2
  else                       
    terrain_type                     GRASS
  endif  
  land_percent                     1
  border_fuzziness                 10
  other_zone_avoidance_distance    7
  land_id                          20
  left_border                      5
  right_border                     5
  top_border                       5
  bottom_border                    5 
}

/* NUISANCE ISLAND 1 */

create_land 
{
  if DESERT_MAP
    terrain_type                     DESERT
  elseif ASIAN_MAP
    terrain_type                     GRASS2
  else                       
    terrain_type                     GRASS
  endif  
  land_percent                     1
  border_fuzziness                 10
  other_zone_avoidance_distance    7
  land_id                          25
  left_border                      5
  right_border                     5
  top_border                       5
  bottom_border                    5 
}

/* NUISANCE ISLAND 2 */

create_land 
{
  if DESERT_MAP
    terrain_type                     GRASS
  elseif ASIAN_MAP
    terrain_type                     GRASS3
  else                       
    terrain_type                     DIRT
  endif  
  land_percent                     1
  border_fuzziness                 10
  other_zone_avoidance_distance    7
  land_id                          25
  left_border                      5
  right_border                     5
  top_border                       5
  bottom_border                    5 
}

/* ****************************************************** */
<TERRAIN_GENERATION>

create_terrain MED_WATER
{
base_terrain WATER
number_of_clumps               20
spacing_to_other_terrain_types 2
land_percent 15
}

create_terrain DEEP_WATER
{
base_terrain MED_WATER
number_of_clumps               10
spacing_to_other_terrain_types 2
land_percent 5
}

/* PRIMARY FOREST */

if DESERT_MAP
  create_terrain PALM_DESERT
{
  base_terrain                   DIRT
  spacing_to_other_terrain_types 5
  land_percent                   9
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ASIAN_MAP
create_terrain PINE_FOREST
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 5
  land_percent                   9
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain FOREST
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 5
  land_percent                   9
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* PRIMARY PATCH */

if DESERT_MAP
   create_terrain DESERT
{
  base_terrain                   DIRT
  number_of_clumps               8
  spacing_to_other_terrain_types 0
  land_percent                   8
  set_scale_by_size
}
elseif ASIAN_MAP
create_terrain GRASS3
{
  base_terrain                   GRASS2
  number_of_clumps               8
  spacing_to_other_terrain_types 0
  land_percent                   6
  set_scale_by_size
}
else
create_terrain DIRT
{
  base_terrain                   GRASS
  number_of_clumps               8
  spacing_to_other_terrain_types 0
  land_percent                   9
  set_scale_by_size
}
endif

/* SECONDARY FOREST */

if DESERT_MAP
create_terrain FOREST
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ASIAN_MAP
create_terrain BAMBOO
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain PALM_DESERT
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* SECONDARY PATCH */

if DESERT_MAP
   create_terrain DIRT3
{
  base_terrain                   DIRT
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
elseif ASIAN_MAP
create_terrain DIRT3
{
  base_terrain                   GRASS2
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
else
create_terrain GRASS3
{
  base_terrain                   GRASS
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
endif

/* TERTIARY PATCH */

if DESERT_MAP
   create_terrain GRASS3
{
  base_terrain                   DIRT
  number_of_clumps               30
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
elseif ASIAN_MAP
create_terrain GRASS
{
  base_terrain                   GRASS2
  number_of_clumps               30
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
else
create_terrain DIRT3
{
  base_terrain                   GRASS
  number_of_clumps               30
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
endif

/* ****************************************************** */
<OBJECTS_GENERATION>

#include_drs land_and_water_resources.inc 54102

create_object DEER
{
   number_of_objects 4
   group_variance 1
   number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    25
   min_distance_group_placement  5
}

if DESERT_MAP
create_object PALMTREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
elseif ASIAN_MAP
create_object BAMBOO_TREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
else
create_object OAKTREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
endif

/* BONUS ISLAND RESOURCES */

create_object GOLD
{
  number_of_groups           2   
  number_of_objects          4
  group_placement_radius     2
  set_tight_grouping
  set_gaia_object_only
  place_on_specific_land_id 20
}

create_object SALMON
{
  number_of_objects                   25
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         2
}

create_object SHORE_FISH
{
  number_of_objects                   30
  set_scaling_to_map_size 
/*  min_distance_group_placement  3 */
  set_gaia_object_only 
}

/* ****************************************************** */

<ELEVATION_GENERATION>

create_elevation        7
{
if DESERT_MAP
  base_terrain                DIRT
elseif ASIAN_MAP
  base_terrain                GRASS2
else           
  base_terrain                GRASS
endif
  number_of_clumps 		14
  number_of_tiles 		1000
  set_scale_by_groups
  set_scale_by_size           
}

/* ****************************************************** */
<CONNECTION_GENERATION>
create_connect_all_players_land
{
  replace_terrain WATER         SHALLOW
  replace_terrain MED_WATER     SHALLOW
  replace_terrain DEEP_WATER    SHALLOW
  terrain_cost    WATER         7
  terrain_cost    MED_WATER     9
  terrain_cost    DEEP_WATER    15
  terrain_cost    FOREST        7
  terrain_cost    BAMBOO        7
  terrain_cost    PALM_DESERT   7
  terrain_cost    SHALLOW       3
  terrain_cost    DESERT        1
  terrain_cost    DIRT          1
  terrain_cost    DIRT          2
  terrain_cost    DIRT          3
  terrain_cost    GRASS         2
  terrain_cost    BEACH         7
  terrain_size    WATER         3       1
  terrain_size    MED_WATER     3       1
  terrain_size    DEEP_WATER    3       1
  terrain_size    GRASS         0       0
  terrain_size    FOREST        0       0
  terrain_size    BAMBOO        0       0
  terrain_size    PALM_DESERT   0       0
  terrain_size    DESERT        0       0
}

