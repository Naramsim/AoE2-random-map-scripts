/* ************ HIGHLAND ************ */
/* 7 JULY 99 */

/* Note: random_map.def is automatically included in every rms script */
ai_info_map_type        HIGHLAND

/* ****************************************************** */
<PLAYER_SETUP>
  random_placement  /* note this is only currently valid entry */

/* ****************************************************** */
<LAND_GENERATION>
base_terrain WATER

start_random
  percent_chance 25
  #define ALPINE_MAP
  percent_chance 25
  #define ASIAN_MAP
  percent_chance 25
  #define FROZEN_MAP
end_random

create_player_lands 
{            
if ALPINE_MAP
  terrain_type                     GRASS2
elseif ASIAN_MAP
  terrain_type                     GRASS2
elseif FROZEN_MAP
  terrain_type                     SNOW
else           
  terrain_type                     GRASS
endif
  land_percent                     100
  base_size                        9
/* randomly choose if we have rivers and to what extent */
  start_random
  percent_chance        60
    set_zone_by_team
  percent_chance        40
    set_zone_randomly  
  end_random
  start_random
  percent_chance       50
    other_zone_avoidance_distance    7
  percent_chance       50
    other_zone_avoidance_distance    10
  end_random
  border_fuzziness                 15
}

/* ****************************************************** */
<TERRAIN_GENERATION>

create_terrain MED_WATER
{
base_terrain WATER
number_of_clumps               6
spacing_to_other_terrain_types 2
land_percent 15
}

create_terrain DEEP_WATER
{
base_terrain MED_WATER
number_of_clumps               10
spacing_to_other_terrain_types 2
land_percent 10
}

/* PRIMARY FOREST LARGE CLUMP */

if ALPINE_MAP
create_terrain PINE_FOREST
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 5
  land_percent                   9
  number_of_clumps               4
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ASIAN_MAP
create_terrain PINE_FOREST
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 5
  land_percent                   9
  number_of_clumps               4
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif FROZEN_MAP
create_terrain SNOW_FOREST
{
  base_terrain                   SNOW
  spacing_to_other_terrain_types 5
  land_percent                   9
  number_of_clumps               4
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain FOREST
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 5
  land_percent                   9
  number_of_clumps               4
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* PRIMARY FOREST SMALL CLUMP */

if ALPINE_MAP
create_terrain PINE_FOREST
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 5
  land_percent                   8
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ASIAN_MAP
create_terrain BAMBOO
{
  base_terrain                   GRASS2
  spacing_to_other_terrain_types 5
  land_percent                   8
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif FROZEN_MAP
create_terrain SNOW_FOREST
{
  base_terrain                   SNOW
  spacing_to_other_terrain_types 5
  land_percent                   8
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain FOREST
{
  base_terrain                   GRASS
  spacing_to_other_terrain_types 5
  land_percent                   8
  number_of_clumps               10
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* PRIMARY PATCH */

if ALPINE_MAP
create_terrain GRASS3
{
  base_terrain                   GRASS2
  number_of_clumps               8
  spacing_to_other_terrain_types 0
  land_percent                   6
  set_scale_by_size
}
elseif ASIAN_MAP
create_terrain GRASS3
{
  base_terrain                   GRASS2
  number_of_clumps               8
  spacing_to_other_terrain_types 0
  land_percent                   6
  set_scale_by_size
}
elseif FROZEN_MAP
create_terrain GRASS_SNOW
{
  base_terrain                   SNOW
  number_of_clumps               8
  spacing_to_other_terrain_types 0
  land_percent                   6
  set_scale_by_size
}
else
create_terrain DIRT3
{
  base_terrain                   GRASS
  number_of_clumps               8
  spacing_to_other_terrain_types 0
  land_percent                   9
  set_scale_by_size
}
endif

/* SECONDARY FOREST */

if ALPINE_MAP
create_terrain FOREST
{
  base_terrain                   GRASS3
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif ASIAN_MAP
create_terrain PINE_FOREST
{
  base_terrain                   GRASS3
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
elseif FROZEN_MAP
create_terrain PINE_FOREST
{
  base_terrain                   GRASS_SNOW
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
else
create_terrain FOREST
{
  base_terrain                   DIRT3
  spacing_to_other_terrain_types 3
  land_percent                   1
  number_of_clumps               3
  set_avoid_player_start_areas     
  set_scale_by_groups
}
endif

/* SECONDARY PATCH */

if ALPINE_MAP
create_terrain DIRT3
{
  base_terrain                   GRASS2
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
elseif ASIAN_MAP
create_terrain DIRT3
{
  base_terrain                   GRASS2
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
elseif FROZEN_MAP
create_terrain DIRT_SNOW
{
  base_terrain                   SNOW
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
else
create_terrain GRASS3
{
  base_terrain                   GRASS
  number_of_clumps               24
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
endif

/* TERTIARY PATCH */

if ALPINE_MAP
create_terrain GRASS
{
  base_terrain                   GRASS2
  number_of_clumps               30
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
elseif ASIAN_MAP
create_terrain GRASS
{
  base_terrain                   GRASS2
  number_of_clumps               30
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
elseif FROZEN_MAP
create_terrain GRASS2
{
  base_terrain                   GRASS_SNOW
  number_of_clumps               30
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
else
create_terrain DIRT3
{
  base_terrain                   GRASS
  number_of_clumps               30
  spacing_to_other_terrain_types 0
  land_percent                   2
  set_scale_by_size
}
endif

/* ****************************************************** */
<OBJECTS_GENERATION>

#include_drs land_resources.inc 54103

create_object SHEEP
{
   number_of_objects 2
   number_of_groups 2
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    19
   min_distance_group_placement  5
}

create_object BOAR
{
   number_of_objects 2
   group_variance    1
   set_loose_grouping
   set_gaia_object_only
   set_place_for_every_player
   min_distance_to_players    35
   min_distance_group_placement  5
}

if ALPINE_MAP
create_object PINETREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
elseif ASIAN_MAP
create_object BAMBOO_TREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
else
create_object OAKTREE
{
  number_of_objects          30
  set_gaia_object_only
  set_scaling_to_map_size
  min_distance_to_players    8
}
endif

create_object SALMON
{
  number_of_objects                   4
  set_scaling_to_map_size
  set_gaia_object_only
  max_distance_to_other_zones         4
}

create_object SHORE_FISH
{
  number_of_objects                   16
  min_distance_group_placement         5
  set_gaia_object_only
  set_scaling_to_map_size
}

/* ****************************************************** */

<ELEVATION_GENERATION>

create_elevation        7
{
if ALPINE_MAP
  base_terrain                GRASS2
elseif ASIAN_MAP
  base_terrain                GRASS2
elseif FROZEN_MAP
  base_terrain                SNOW
else           
  base_terrain                GRASS
endif
  number_of_clumps 		14
  number_of_tiles 		2000
  set_scale_by_groups
  set_scale_by_size           
}

<CLIFF_GENERATION>

min_number_of_cliffs 5
max_number_of_cliffs 8
min_length_of_cliff  4
max_length_of_cliff  10
cliff_curliness      10
min_distance_cliffs  3     


/* ****************************************************** */
<CONNECTION_GENERATION>
create_connect_all_players_land
{
if FROZEN_MAP
  replace_terrain WATER         ICE
  replace_terrain MED_WATER     ICE
  replace_terrain DEEP_WATER    ICE
else
  replace_terrain WATER         SHALLOW
  replace_terrain MED_WATER     SHALLOW
  replace_terrain DEEP_WATER    SHALLOW
endif
  terrain_cost    WATER         7
  terrain_cost    MED_WATER     9
  terrain_cost    DEEP_WATER    15
  terrain_cost    FOREST        7
  terrain_cost    PINE_FOREST   7
  terrain_cost    SNOW_FOREST   7
  terrain_cost    ICE           3
  terrain_cost    SHALLOW       3
  terrain_cost    DESERT        1
  terrain_cost    DIRT2         1
  terrain_cost    DIRT3         1
  terrain_cost    GRASS         2
  terrain_cost    SNOW          2
  terrain_cost    GRASS2        2
  terrain_cost    GRASS3        2
  terrain_cost    GRASS_SNOW    2
  terrain_cost    DIRT_SNOW     2
  terrain_cost    BEACH         7
  terrain_size    WATER         2       1
  terrain_size    MED_WATER     2       1
  terrain_size    DEEP_WATER    2       1
  terrain_size    GRASS         0       0
  terrain_size    SNOW          0       0
  terrain_size    GRASS2        0       0
  terrain_size    GRASS3        0       0
  terrain_size    GRASS_SNOW    0       0
  terrain_size    FOREST        0       0
  terrain_size    PINE_FOREST   0       0
  terrain_size    DESERT        0       0
  terrain_size    DIRT2         0       0
  terrain_size    DIRT3         0       0
}
